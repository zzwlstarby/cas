buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url "https://repo.spring.io/milestone/" }
        maven { url "https://repo.spring.io/libs-milestone" }
        maven { url "https://oss.jfrog.org/artifactory/oss-snapshot-local" }
        maven { url "https://repo.spring.io/snapshot" }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${project.springBootVersion}"
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url "https://oss.jfrog.org/artifactory/oss-snapshot-local" }
    maven { url 'https://build.shibboleth.net/nexus/content/repositories/releases/' }
    maven { url "https://repo.spring.io/plugins-release/" }
    maven { url "https://repo.spring.io/milestone/" }
    maven { url "https://repo.spring.io/libs-milestone" }
    maven { url "https://repo.spring.io/snapshot" }
}

apply from: "https://dl.bintray.com/scalding/generic/waroverlay.gradle"

apply plugin: "war"
apply plugin: "org.springframework.boot"

def currentBranch = "master"
def currentVersion = "$System.env.casVersion"
if (!currentVersion.contains("RC")) {
    def matcher = currentVersion =~ /(\d+\.\d+\.).+/
    if (matcher.find()) {
        currentBranch = matcher.group(1) + "x"
    }
}
println "Using CAS branch [${currentBranch}] to handle overrides"
apply from: "https://raw.githubusercontent.com/apereo/cas/${currentBranch}/gradle/overrides.gradle"

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

war {
    includeWarJars = true
    entryCompression = ZipEntryCompression.STORED
}

springBoot {
    mainClassName = "org.apereo.cas.CasSpringBootAdminServerWebApplication"
}

bootWar {
	if (project.hasProperty('executable')) {
    	launchScript()
	}

	archiveName 'casbootadminserver.war'
    baseName 'casbootadminserver'

    excludeDevtools = false
}

bootRun {
    sourceResources sourceSets.main
    classpath = sourceSets.main.runtimeClasspath
}

dependencies {
    compile group: "org.apereo.cas", name: "cas-server-webapp-bootadmin-server", version: "${project.'cas.version'}", changing: true
}

task wrapper(type: Wrapper, description: "Update the Gradle wrapper") {
    gradleVersion = "${project.gradleVersion}"
}
